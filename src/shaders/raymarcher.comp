#version 430 core

//Output texture
layout(binding = 0, rgba32f) uniform writeonly image2D framebuffer;

//Object inputs
//layout(std430, binding = 1) buffer ObjectsBuffer { Object objects[]; };

//Skybox
layout(binding = 1) uniform samplerCube skybox;

//Camera uniforms

uniform vec3 eye;
uniform vec3 ray00;
uniform vec3 ray01;
uniform vec3 ray10;
uniform vec3 ray11;

struct material
{
  vec4 color;
  float reflectivity;
  float refractivity;
  float transparency;
};

uniform float BAILOUT = 2.0;
uniform float POWER = 8.0;
uniform float ITERATIONS = 500;
uniform float MAX_RAY_STEPS = 1000;

#define MINIMUM_DISTANCE 0.0001
#define SCALE 20.0
#define SIERPINSKI_ITERATIONS 5

#define FLOAT_COMPARISON_PRECISION 0.000001
#define AMBIENT_STRENGTH 0.1
#define SPECULAR_STRENGTH 0.5
#define BIAS 0.001

const vec3 sunLightColor = vec3(1.0, 1.0, 1.0); 
const vec3 sunLight = normalize(vec3(1.0, 1.0, 1.0)); //Direction for the sunlight

const vec4 skyColor = {0.3, 0.7, 0.9, 1.0};

//Orbit coloring
const vec4 colorHigh = {0.3, 1.0, 0.3, 1.0};//{0.0, 0.7, 1.0, 1.0};//{0.4, 0.7, 0.8, 1.0};
const vec4 colorLow = {0.4, 0.2, 0.1, 1.0};//{0.4, 0.7, 0.8, 1.0};
const float distanceMax = 1.0;
const float distanceMin = 0.7;

const vec3 xDir = {1.0, 0, 0};
const vec3 yDir = {0, 1.0, 0};
const vec3 zDir = {0, 0, 1.0};

//Support structs
struct rayObj
{
  vec3 dir;
  vec3 ori;
};

//Helper functions
bool fuzzy_equals(float a, float b)
{
  if (abs(a - b) < FLOAT_COMPARISON_PRECISION) return true;
  return false;
}

bool fuzzy_equals(vec3 a, vec3 b)
{
  if (fuzzy_equals(a.x, b.x) && fuzzy_equals(a.y, b.y) && fuzzy_equals(a.z, b.z)) return true;
  return false;
}

void swap(float a, float b)
{
  float temp = a;
  a = b;
  b = temp;
}

vec3 getRayPos(rayObj ray, float t)
{
  return ray.ori + ray.dir * t;
}

//Distance estimators

float mandelbulbDistanceEst(vec3 pos, out float distance) {
	vec3 z = pos;
	float dr = 1.0;
	float r = 0.0;
	for (int i = 0; i < ITERATIONS ; i++) {
		r = length(z);
    distance = min(distance, r);
		if (r>BAILOUT) break;
		
		// convert to polar coordinates
		float theta = acos(z.z/r);
		float phi = atan(z.y,z.x);
		dr =  pow(r, POWER - 1.0)*POWER*dr + 1.0;
		
		// scale and rotate the point
		float zr = pow(r, POWER);
		theta = theta*POWER;
		phi = phi*POWER;
		
		// convert back to cartesian coordinates
		z = zr*vec3(sin(theta)*cos(phi), sin(phi)*sin(theta), cos(theta));
		z+=pos;
	}
	return 0.5*log(r)*r/dr;
}

float sierpinskiDistanceEst(vec3 z)
{
    float r;
    int n = 0;
    while (n < SIERPINSKI_ITERATIONS) {
       if(z.x+z.y<0) z.xy = -z.yx; // fold 1
       if(z.x+z.z<0) z.xz = -z.zx; // fold 2
       if(z.y+z.z<0) z.zy = -z.yz; // fold 3	
       z = z*SCALE - vec3(1,1,1)*(SCALE-1.0);
       n++;
    }
    return (length(z) ) * pow(SCALE, -float(n));
}

float sphereDistanceEst(vec3 p)
{
  //return max(0.0, length(point)-1.0);
  p.xy = vec2(mod(p.x, 1.0), mod(p.y, 1.0)) - 0.5;
  return length(p)-0.3;
}

float distanceEst(vec3 pos, out float distance)
{
  return mandelbulbDistanceEst(pos, distance);
}

//Lightning calculations

vec4 calculateLight(vec3 normal, vec3 dir, vec3 pos)
{ //Direction for the sunlight
    // ambient
    vec3 ambient = AMBIENT_STRENGTH * sunLightColor;
  	
    // diffuse 
    float diff = max(dot(normal, sunLight), 0.0);
    vec3 diffuse = diff * sunLightColor;
    
    // specular
    vec3 reflectDir = reflect(sunLight, normal);  
    float spec = pow(max(dot(dir, reflectDir), 0.0), 32);
    vec3 specular = SPECULAR_STRENGTH * spec * sunLightColor;

    return vec4(diffuse + specular + ambient, 1.0);

}

vec3 findNormal(vec3 pos)
{
    vec3 normal;
    float orbitDistance;
    normal.x = distanceEst(pos+xDir*MINIMUM_DISTANCE, orbitDistance)-distanceEst(pos-xDir*MINIMUM_DISTANCE, orbitDistance);
    normal.y = distanceEst(pos+yDir*MINIMUM_DISTANCE, orbitDistance)-distanceEst(pos-yDir*MINIMUM_DISTANCE, orbitDistance);
    normal.z = distanceEst(pos+zDir*MINIMUM_DISTANCE, orbitDistance)-distanceEst(pos-zDir*MINIMUM_DISTANCE, orbitDistance);
    return normalize(normal);
}

vec4 colorOrbit(vec4 colorHigh, vec4 colorLow, float distance)
{
    distance = (distance - distanceMin) / (distanceMax - distanceMin);
    return mix(colorLow, colorHigh, clamp(distance, 0.0, 1.0));
}

//Main march function
vec4 march(rayObj ray) {
  int steps;
  bool hit = false;
  float dist = 100000;
	for (steps = 0; steps < MAX_RAY_STEPS; steps++) {
		float distance = distanceEst(ray.ori, dist);
		ray.ori += distance * ray.dir;
		if (distance < MINIMUM_DISTANCE) 
    {
      hit = true;
      break;
    }
	}

  if (hit)
    return colorOrbit(colorHigh, colorLow, dist) * calculateLight(findNormal(ray.ori), ray.dir, ray.ori);
  else
    return skyColor;
}

layout (local_size_x = 16, local_size_y = 8) in;
void main(void) {
  ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
  ivec2 size = imageSize(framebuffer);
  if (pix.x >= size.x || pix.y >= size.y) {
    return;
  }
  vec2 pos = vec2(pix) / vec2(size.x - 1, size.y - 1);
  vec3 dir = normalize(mix(mix(ray00, ray01, pos.y), mix(ray10, ray11, pos.y), pos.x));
  rayObj ray = rayObj(dir, eye);
  vec4 color = march(ray);
  imageStore(framebuffer, pix, color);
}
